         MACRO ,                                STRUCT/COMPUT MACRO SET
&GPR     EQUR  &VALUE
.**********************************************************************
.*COPYRIGHT GESELLSCHAFT FUER MATHEMATIK UNF DATENVERARBEITUNG MBH 1985
.*LICENSED MATERIAL --- PROGRAM PROPERTY OF GMD-Z1.BN
.**********************************************************************
.*                                            PAHL (GMD-IST/RZ) 00JAN74
.*  REGISTER-SYMBOL DEFINITION                                  12MAY75
.*       ALL GPR'S MUST BE DEFINED VIA THIS MACRO, SO THAT THE MACROS
.*       OF STRUCT/COMPUT MACRO (AND ESPECIALLY #USE AND #DROP)
.*       CAN RECOGNISE A REGISTER (FROM AN ORDINARY SYMBOL) AND GET
.*       THE NUMERICAL VALUE (FOR REGISTER (MIS)USE CONTROL).
.*       NO MACRO OTHER THAN     LET0Q      MAY KNOW THE GPR DEFINI-
.*       TION TECHNIQUE.  LET0Q IS EXCEPTED FOR PERFORMANCE REASONS.
.*       IN ORDER TO RECOGNIZE A SYMBOL AS REGISTER AND TO GET THE REGI
.*       STER NUMBER, THIS MACRO MUST BE CALLED (EXCEPTION: LET0Q),
.*       IN THE FORM  "  EQUR SYMBOL", RETURNING IN GBLA &EQUR 1-16 FOR
.*       GPR'S 0-15 AND SOMETHING LT. 1 IF SYMBOL IS NOT DEFINED AS
.*       GPR (BY A PREVIOUS "SYMBOL EQUR NUMBER" MACRO CALL).
.*
.*  PARAMETERS FOR DEFINITION:
.*  &GPR    : GPR NAME,  MAY BEGIN WITH R,Q, OR X   (CHANGED 22JUN76)
.*  &VALUE  : ABS.EXPR<16 | GPR-NAME PREVIOUSLY DEFINED
.*  PARAMETERS FOR INQUIRY:
.*  &GPR    : OMITTED
.*  &VALUE  : SYMBOL IN QUESTION (REGISTER-SYMBOL OR NOT)
.*
         GBLA  &EQUR
&GPR     SASLIST MACRO,'EQUR'
         AIF   ('&GPR' NE '').DEFINE
.*
.*  GET REGISTER NUMBER FOR REGISTER-SYMBOL &VALUE
.*
&EQUR    SETA  0                       'NOT DEFINED AS REGISTER'
         AIF   (T'&VALUE NE '@').MEND  AS WE SAID
&EQUR    SETA  L'&VALUE+1              REG NUMBER+1
         AGO  .MEND
.*
.*  DEFINE NEW REGISTER-SYMBOL &GPR
.*
.DEFINE  AIF   (T'&GPR NE '@').DEF1
         AIF   (L'&GPR EQ &VALUE).MEND AVOID DUPLIC SAME DEFN
.DEF1    AIF   (T'VALUE EQ '@').DEF2
.*-PR    DSECT ,                       REG IS SYMBOL FOR 0(REG)
&GPR     EQU   &VALUE,&VALUE,C'@'
.*-      USING @&GPR,&GPR
.*-      AIF   ('&GPR'(1,1) EQ 'R' OR '&GPR'(1,1) EQ 'Q' OR '&GPR'(1,1)-
                EQ 'X').TESTR0
.*-      MNOTE 04,'****** EQUR: GPR NAME SHOULD START WITH "R", "Q", OR-
                "X".'
.TESTR0  AIF   (L'&GPR NE 1 OR '&GPR' EQ '@0').MEND
.*       MNOTE 04,'****** EQUR: GPR 0 MAY BE NAMED "@0" ONLY.'
         AGO   .MEND
.DEF2    ANOP
&GPR     EQU   L'&VALUE,L'&VALUE,C'@'
         AGO   .TESTR0
.MEND    SASLIST MACEND
         MEND
