         MACRO
         CONSTX
.**********************************************************************
.*COPYRIGHT GESELLSCHAFT FUER MATHEMATIK UNF DATENVERARBEITUNG MBH 1985
.*LICENSED MATERIAL --- PROGRAM PROPERTY OF GMD-Z1.BN
.**********************************************************************
.*
.*  OPEN/RESUME (LOCAL) CONSTANTS DECLARATION PART OF PROCEDURE
.*        "PROC" ... "DATA" DATA-DCL "CONST" DATA-DCL
.*        "CODE" INSTRUCTIONS ...
.*
         GBLA  &PRC
         GBLC  &PRCTYPE(04),&PRCCODE(04),&PRCDATA(04),&PRCCONS(04)
         LCLC  &C
         LCLA  &PRCTEMP
.SASLIST SASLIST MACRO,'CONST'
&PRCTEMP SETA  &PRC
.LOOP    AIF   ('&PRCCONS(&PRCTEMP)'     NE '').GEN
&PRCTEMP SETA  &PRCTEMP-1
         AIF   (&PRCTEMP GE 1).LOOP
&PRCTEMP SETA  &PRC
&C       SETC  '&PRCTYPE(&PRCTEMP)'(1,1)
         MNOTE 0,'****** CONST: TYPE=&C OF CURRENT PROC DOES NOT ALLOW -
               LOCAL DATA.'
         AGO   .GEN
.GEN     ANOP
         AIF   (&PRCTEMP EQ &PRC).NOWARN
&C       SETC  '&PRCTYPE(&PRC)'(1,1)
         MNOTE 0,'****** CONST: TYPE=&C OF CURRENT PROC DOES NOT ALLOW -
               LOCAL DATA. DATA SECTION OF STRUCTURE LEVEL &PRCTEMP RES-
               UMED, INSTEAD.'
.NOWARN  ANOP
.*
.*  OPEN LOCAL AREA IF REQUIRED
.*
.*_      MNOTE '***************'
.*_RESUME LOCAL CONSTANTS DECLARATIONS FOR CURRENT PROCEDURE
&PRCCODE(&PRC) LOCTR ,
&PRCCONS(&PRC) LOCTR ,
.MEND    SASLIST MACEND
         MEND
