         MACRO
&L       TESTRET &RETURN,&PNAME
.**********************************************************************
.*COPYRIGHT GESELLSCHAFT FUER MATHEMATIK UNF DATENVERARBEITUNG MBH 1985
.*LICENSED MATERIAL --- PROGRAM PROPERTY OF GMD-Z1.BN
.**********************************************************************
.*
.*   FIND OR TEST RETURN REGISTER OF INTERNAL PROCEDURES
.*   SUBMACRO OF $PROC,PROC$,ICALL
.*
         GBLA  &ICALMAX
         GBLC  &ICALNAM(30),&ICALRET(30),&BLKNAME(30)
         LCLA  &ICALNDX
         AIF   ('&L' EQ '').A1
&L       DS    0H
.A1      ANOP
&ICALNDX SETA  1
.TSTNEXT AIF   (&ICALNDX GT &ICALMAX).NOTFND
         AIF   ('&PNAME'         EQ '&ICALNAM(&ICALNDX)').PROCFND
&ICALNDX SETA  &ICALNDX+1
         AGO   .TSTNEXT
.*   PROC NAME FOUND. COMPARE CALLERS RET REG WITH &RETURN
.PROCFND AIF   ('&RETURN' NE '').PROCCMP    FIND OR TEST ?
         EQUR  R&ICALRET(&ICALNDX)
         AGO  .MEND
.PROCCMP AIF   ('&RETURN' EQ '&ICALRET(&ICALNDX)').MEND
         MNOTE 8,'*****  DIFFERENT RETURN REGS FOR &ICALNAM(&ICALNDX): -
               &ICALRET(&ICALNDX) <-> &RETURN'
         AGO   .MEND
.NOTFND  AIF   ('&RETURN' NE '').NOTFNDC    FIND OR TEST ?
         EQUR  R14
         AGO   .MEND
.NOTFNDC AIF   ('&RETURN' EQ '@14' OR '&RETURN' EQ '14').MEND
&ICALMAX SETA  &ICALMAX+1
&ICALNAM(&ICALMAX) SETC '&PNAME'
&ICALRET(&ICALMAX) SETC '&RETURN'
.MEND    MEND
