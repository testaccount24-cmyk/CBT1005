         MACRO                                         STRUCT MACRO SET
         CASE$ &LABEL
.**********************************************************************
.*COPYRIGHT GESELLSCHAFT FUER MATHEMATIK UNF DATENVERARBEITUNG MBH 1985
.*LICENSED MATERIAL --- PROGRAM PROPERTY OF GMD-Z1.BN
.**********************************************************************
.*                 ADAPTED FROM IF$           PAHL (GMD-IST/RZ) 00JAN73
.*  "(CASE" <"ON" COND "DO" BLOCK>* <"ELSE" BLOCK> "CASE)"      27MAR75
.*       END "CASE" CLAUSE
.*                                       SYLVESTER (GMD-RZ.BN)  19OCT79
         GBLA  &BLK,&BLKDCNT(30)
         GBLC  &BLKTYPE(30),&BLKNAME(30),&BLKID(30)
         GBLB  &BLKXCPT(30)
         LCLC  &C
&LABEL   SASLIST MACRO,'CASE)'
.*
         AIF   ('&LABEL' NE '' AND '&LABEL' NE '&BLKNAME(&BLK)').Y0
.*
         AIF   (NOT &BLKXCPT(&BLK)).NOXCPT
         CODEX ,
&BLKXCPT(&BLK) SETB 0
         IF0BC 15,@@Z&BLKID(&BLK)
         CODEX ,
.NOXCPT ANOP
&C       SETC  '&BLKTYPE(&BLK)'
         AIF   ('&C' NE 'CD').ELS0      WAS IN ELSE PART
@@F&BLKID(&BLK)&BLKDCNT(&BLK) DS  0H
@@Y&BLKID(&BLK)&BLKDCNT(&BLK) DS  0H
@@V&BLKID(&BLK)&BLKDCNT(&BLK) DS  0H
&BLKTYPE(&BLK) SETC 'CE'
&C       SETC 'CE'
.ELS0    ANOP
@@X&BLKID(&BLK)&BLKDCNT(&BLK) DS  0H
         AIF   ('&C' NE 'CB').ELSE      WAS IN ELSE PART
@@F&BLKID(&BLK) DS 0H
@@Y&BLKID(&BLK) DS 0H
@@V&BLKID(&BLK) DS 0H
@@X&BLKID(&BLK) DS 0H
.ZDEF    ANOP
@@Z&BLKID(&BLK) DS  0H                   END OF "IF" CLAUSE
&BLK     SETA  &BLK-1                  POP
         AGO  .MEND
.ELSE    AIF   ('&C' EQ 'CE').ZDEF
.**********************************************************************
.YPART   AIF   ('&BLKTYPE(&BLK)'(1,1) NE 'C').Y0
         MNOTE 08,'****** CASE): DO/ELSE PART(S) MISSING'
.*       AGO   .TEST
.Y0      BLK0TEST CASE,(T,E),&LABEL
.MEND    SASLIST MACEND
         MEND
